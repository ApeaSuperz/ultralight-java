plugins {
    id 'maven-publish'
    id "net.minecrell.licenser" version "0.4.1" apply false
    id "signing"
}

group 'com.labymedia'
apply from: 'version.gradle'

apply from: 'ci.gradle'

def getSigningProperty(String name) {
    return System.getenv("SIGNING_${name}")
}

ext.commonPublish = { Project currentProject, Closure config ->
    currentProject.publishing {
        publications {
            mavenJava(MavenPublication) {
                from currentProject.components.java

                configure(it, config)

                version = rootProject.version

                pom {
                    url = "https://github.com/LabyMod/ultralight-java"

                    licenses {
                        license {
                            name = "LGPLv3"
                            url = "https://www.gnu.org/licenses/lgpl-3.0.txt"
                            distribution = "repo"
                        }
                    }

                    developers {
                        developer {
                            id = "LabyMedia"
                            email = "contact@labymod.net"
                        }
                    }

                    scm {
                        connection = "scm:git:git://github.com/LabyMod/ultralight-java.git"
                        developerConnection = "scm:git:ssh://github.com/LabyMod/ultralight-java.git"
                        url = "https://github.com/LabyMod/ultralight-java"
                    }
                }
            }
        }
    }

    if(rootProject.hasProperty("enableSigning")) {
        currentProject.signing {
            def signingKey = getSigningProperty("KEY")
            def signingPassword = getSigningProperty("PASSWORD")

            useInMemoryPgpKeys(signingKey, signingPassword)

            sign currentProject.publishing.publications.mavenJava
        }
    }
}

subprojects {
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    pluginManager.withPlugin("java") {
        apply plugin: "net.minecrell.licenser"
        apply plugin: "signing"

        license {
            header = rootProject.file("LICENSE_HEADER")

            ext {
                author = "LabyMedia"
                year = Calendar.getInstance(TimeZone.getTimeZone("UTC")).get(Calendar.YEAR)
            }
        }

        java {
            withSourcesJar()
            withJavadocJar()
        }
    }
}
